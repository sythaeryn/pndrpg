#-----------------------------------------------------------------------------
#Platform specifics

FIND_PACKAGE(ZLIB)

INCLUDE_DIRECTORIES(${CMAKE_CURRENT_SOURCE_DIR}/common/src )
INCLUDE_DIRECTORIES(${CMAKE_CURRENT_SOURCE_DIR}/common )
SET(RZ_SERVER_SRC_DIR ${CMAKE_CURRENT_SOURCE_DIR}/server/src)

ADD_SUBDIRECTORY(common)

IF(WITH_RYZOM_CLIENT)
  IF(WITH_LUA51)
    FIND_PACKAGE(Lua51 REQUIRED)
  ELSE(WITH_LUA51)
    FIND_PACKAGE(Lua50 REQUIRED)
  ENDIF(WITH_LUA51)
  FIND_PACKAGE(Luabind REQUIRED)
  FIND_PACKAGE(CURL REQUIRED)

  IF(WIN32 OR CURL_LIBRARIES MATCHES "\\.a")
    SET(CURL_STATIC ON)
  ENDIF(WIN32 OR CURL_LIBRARIES MATCHES "\\.a")

  IF(CURL_STATIC)
    SET(CURL_DEFINITIONS -DCURL_STATICLIB)

    FIND_PACKAGE(OpenSSL QUIET)

    IF(OPENSSL_FOUND)
      SET(CURL_INCLUDE_DIRS ${CURL_INCLUDE_DIRS} ${OPENSSL_INCLUDE_DIR})
      SET(CURL_LIBRARIES ${CURL_LIBRARIES} ${OPENSSL_LIBRARIES})
    ENDIF(OPENSSL_FOUND)

    # CURL Macports version depends on libidn and libintl too
    IF(APPLE)
      FIND_LIBRARY(IDN_LIBRARY idn)
      FIND_LIBRARY(INTL_LIBRARY intl)

      SET(CURL_LIBRARIES ${CURL_LIBRARIES} ${IDN_LIBRARY} ${INTL_LIBRARY})
    ENDIF(APPLE)
  ENDIF(CURL_STATIC)

  FIND_PACKAGE(Libwww REQUIRED)

  ADD_SUBDIRECTORY(client)
ENDIF(WITH_RYZOM_CLIENT)

IF(WITH_RYZOM_TOOLS)
  ADD_SUBDIRECTORY(tools)
ENDIF(WITH_RYZOM_TOOLS)

IF(WITH_RYZOM_SERVER)
  FIND_PACKAGE(MySQL REQUIRED)
  ADD_SUBDIRECTORY(server)
ENDIF(WITH_RYZOM_SERVER)
